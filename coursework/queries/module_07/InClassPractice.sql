USE AP;

-- Example 1
DECLARE @InvoiceId INT;
BEGIN TRY
  BEGIN TRAN;
    INSERT INVOICES
      VALUES (34,'ZXA-080','2020-03-01',14092.59,0,0,3,'2020-03-31',NULL);
    SET @InvoiceId = @@IDENTITY;
    INSERT InvoiceLineItems
      VALUES (@InvoiceId,1,160,4447.23,'HW UPGRADE');
    INSERT InvoiceLineItems
      VALUES (@InvoiceId,2,167,9645.36,'OS UPGRADE');
  COMMIT TRAN;
END TRY
BEGIN CATCH
  ROLLBACK TRAN;
END CATCH;

-- Example 2
BEGIN TRAN;
DELETE INVOICES WHERE VendorID = 34;
IF @@ROWCOUNT > 1
  BEGIN
    ROLLBACK TRAN;
    PRINT 'MORE INVOICES THAN EXPECTED. DELETIONS ROLLED BACK.';
  END;
ELSE
BEGIN
  COMMIT TRAN;
  PRINT 'DELETIONS COMMITTED TO THE DATABASE.';
END;

-- Example 3
BEGIN TRAN;
  PRINT 'FIRST TRAN @@TRANCOUNT: ' + CONVERT(VARCHAR, @@TRANCOUNT);
  DELETE INVOICES;
  BEGIN TRAN;
    PRINT 'SECOND TRAN @@TRANCOUNT: ' +
    CONVERT(VARCHAR, @@TRANCOUNT);
    DELETE VENDORS;
  COMMIT TRAN; -- THIS COMMIT DECREMENTS @@TRANCOUNT.
    -- IT DOESN'T COMMIT 'DELETE VENDORS'.
  PRINT 'COMMIT @@TRANCOUNT: ' +
  CONVERT(VARCHAR,@@TRANCOUNT);
ROLLBACK TRAN;
PRINT 'ROLLBACK @@TRANCOUNT: ' +
CONVERT(VARCHAR,@@TRANCOUNT);
PRINT ' ';
DECLARE @VendorCount INT,
@InvoiceCount INT;
SELECT @VendorCount = COUNT (*) FROM VENDORS;
SELECT @InvoiceCount = COUNT (*) FROM INVOICES;
PRINT 'VENDORS COUNT: ' + CONVERT (VARCHAR, @VendorCount);
PRINT 'INVOICES COUNT: ' + CONVERT (VARCHAR, @InvoiceCount);

-- Example 4
IF OBJECT_ID('TEMPDB..#VendorCopy') IS NOT NULL
  DROP TABLE TEMPDB.. #VendorCopy;
SELECT VendorId, VendorName INTO #VendorCopy FROM VENDORS
  WHERE VendorId < 5;
BEGIN TRAN;
  DELETE #VendorCopy WHERE VendorId = 1;
  SAVE TRAN VENDOR1;
    DELETE #VendorCopy WHERE VendorId = 2;
    SAVE TRAN VENDOR2;
      DELETE #VendorCopy WHERE VendorId = 3;
      SELECT * FROM #VendorCopy;

      -- Still only one transaction ðŸ¤”
      PRINT CONVERT(VARCHAR,@@TRANCOUNT);
    ROLLBACK TRAN VENDOR2;
    SELECT * FROM #VendorCopy;
  ROLLBACK TRAN VENDOR1;
  SELECT * FROM #VendorCopy;
COMMIT TRAN;
SELECT * FROM #VendorCopy;
